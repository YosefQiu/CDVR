struct SparsePoint {
    x: f32,
    y: f32,
    value: f32,
    padding: f32
};

struct Uniforms {
    // 第一组：16字节对齐的float4
    minValue: f32,
    maxValue: f32,
    gridWidth: f32,
    gridHeight: f32,
    
    // 第二组：16字节对齐的uint4  
    totalNodes: u32,
    totalPoints: u32,
    numLevels: u32,
    interpolationMethod: u32,
    
    // 第三组：16字节对齐，包含searchRadius和padding
    searchRadius: f32,
    padding1: f32,
    padding2: f32,
    padding3: f32,
};

struct MyKNNResult {
    indices: array<u32, 3>,
    distances: array<f32, 3>,
    values: array<f32, 3>,
    count: u32,
};


struct GPUPoint {
    x: f32,
    y: f32,
    value: f32,
    padding: f32
};



@group(0) @binding(0) var outputTexture: texture_storage_2d<rgba16float, write>;
@group(0) @binding(1) var<uniform> uniforms: Uniforms;
@group(0) @binding(2) var<storage, read> sparsePoints: array<SparsePoint>;
@group(1) @binding(0) var inputTF: texture_2d<f32>;
@group(2) @binding(0) var<storage, read> kdTreePoints: array<GPUPoint>;

fn getColorFromTF(normalizedValue: f32) -> vec4<f32> {
    let tfWidth = textureDimensions(inputTF).x;
    let texelX = clamp(i32(normalizedValue * f32(tfWidth)), 0, i32(tfWidth) - 1);
    let texelCoord = vec2<i32>(texelX, 0);
    return textureLoad(inputTF, texelCoord, 0);
}
fn distance2D(x1: f32, y1: f32, x2: f32, y2: f32) -> f32 {
    let dx = x1 - x2;
    let dy = y1 - y2;
    return sqrt(dx * dx + dy * dy);
}

fn distanceVec2(p1: vec2<f32>, p2: vec2<f32>) -> f32 {
    return distance(p1, p2);
}

// 安全的KNN搜索函数
fn findKNN(dataPos: vec2<f32>, k: u32) -> MyKNNResult {
    var result: MyKNNResult;
    result.count = 0u;
    
    // 初始化所有数组，使用最大搜索半径
    for (var i = 0u; i < 3u; i++) {
        result.distances[i] = 3.40282347e+38;
        result.indices[i] = 0u;
        result.values[i] = 0.0;
    }
    
    for (var i = 0u; i < uniforms.totalPoints; i++) {
        let point = sparsePoints[i];
        let pointPos = vec2<f32>(point.x, point.y);
        let dist = distanceVec2(dataPos, pointPos);

        if (dist < 3.40282347e+38) {
            // 安全地插入点
            var inserted = false;
            
            // 检查是否应该插入以及插入位置
            for (var j = 0u; j < min(k, 3u); j++) {
                if (j >= result.count || dist < result.distances[j]) {
                    // 安全地后移元素
                    var moveCount = min(result.count, min(k, 3u) - 1u);
                    for (var moveIdx = moveCount; moveIdx > j; moveIdx--) {
                        if (moveIdx < 3u && moveIdx > 0u && (moveIdx - 1u) < 3u) {
                            result.distances[moveIdx] = result.distances[moveIdx - 1u];
                            result.indices[moveIdx] = result.indices[moveIdx - 1u];
                            result.values[moveIdx] = result.values[moveIdx - 1u];
                        }
                    }
                    
                    // 插入新元素
                    if (j < 3u) {
                        result.distances[j] = dist;
                        result.indices[j] = i;
                        result.values[j] = point.value;
                        inserted = true;
                    }
                    break;
                }
            }
            
            if (inserted && result.count < min(k, 3u)) {
                result.count++;
            }
        }
    }
    
    return result;
}

// 最近邻插值
fn nearestNeighborInterpolation(dataPos: vec2<f32>) -> f32 {
    var minDist = 3.40282347e+38;
    var nearestValue = 0.0;
    var found = false;
    
    for (var i = 0u; i < uniforms.totalPoints; i++) {
        let point = sparsePoints[i];
        let pointPos = vec2<f32>(point.x, point.y);
        let dist = distanceVec2(dataPos, pointPos);
        
        if (dist < minDist) {
            minDist = dist;
            nearestValue = point.value;
            found = true;
        }
    }
    
    if (found) {
        return nearestValue;
    }
    return -1.0;
}

// KNN平均插值
fn knnAverageInterpolation(dataPos: vec2<f32>) -> f32 {
    let knn = findKNN(dataPos, 3u);
    
    if (knn.count == 0u) {
        return -1.0;
    }
    
    var sum = 0.0;
    // 展开循环避免动态索引
    if (knn.count >= 1u) {
        sum += knn.values[0];
    }
    if (knn.count >= 2u) {
        sum += knn.values[1];
    }
    if (knn.count >= 3u) {
        sum += knn.values[2];
    }
    
    return sum / f32(knn.count);
}

// KNN质心插值（反距离加权）
fn knnCentroidInterpolation(dataPos: vec2<f32>) -> f32 {
    let knn = findKNN(dataPos, 3u);
    
    if (knn.count == 0u) {
        return -1.0;
    }
    
    // 检查重合点
    if (knn.distances[0] < 0.0001) {
        return knn.values[0];
    }
    
    var weightedSum = 0.0;
    var weightSum = 0.0;
    
    // 展开循环避免动态索引
    if (knn.count >= 1u) {
        let weight = 1.0 / (knn.distances[0] * knn.distances[0]);
        weightedSum += knn.values[0] * weight;
        weightSum += weight;
    }
    if (knn.count >= 2u) {
        let weight = 1.0 / (knn.distances[1] * knn.distances[1]);
        weightedSum += knn.values[1] * weight;
        weightSum += weight;
    }
    if (knn.count >= 3u) {
        let weight = 1.0 / (knn.distances[2] * knn.distances[2]);
        weightedSum += knn.values[2] * weight;
        weightSum += weight;
    }
    
    return weightedSum / weightSum;
}

fn bruteNearestKD(query: vec2<f32>) -> f32 {
    var minDist = 1e30;
    var bestValue = 0.0;
    for (var i = 0u; i < uniforms.totalNodes; i = i + 1u) {
        let p = kdTreePoints[i];
        let d = distance2D(query.x, query.y, p.x, p.y);
        if (d < minDist) {
            minDist = d;
            bestValue = p.value;
        }
    }
    return bestValue;
}


// 二进制树辅助函数（对应CPU的BinaryTree）
fn levelOf(nodeID: i32) -> i32 {
    // 对应CPU代码中的 BinaryTree::levelOf
    // int k = 63 - __builtin_clzll(nodeID + 1);
    // WGSL中没有clz函数，需要手动实现
    var temp = nodeID + 1;
    var k = 0;
    while (temp > 1) {
        temp = temp >> 1;
        k = k + 1;
    }
    return k;
}

// 编码和解码函数（替代CPU的BaseCandidateList，使用两个u32替代u64）
struct EncodedEntry {
    distBits: u32,    // 高32位：距离
    pointIDBits: u32, // 低32位：点ID
};

fn floatAsUint(f: f32) -> u32 {
    return bitcast<u32>(f);
}

fn uintAsFloat(u: u32) -> f32 {
    return bitcast<f32>(u);
}

fn encode(dist: f32, pointID: i32) -> EncodedEntry {
    // 对应CPU代码：(uint64_t(float_as_uint(f)) << 32) | uint32_t(i)
    var entry: EncodedEntry;
    entry.distBits = floatAsUint(dist);
    entry.pointIDBits = bitcast<u32>(pointID);
    return entry;
}

fn decodeDist2(entry: EncodedEntry) -> f32 {
    // 对应CPU代码：uint_as_float(v >> 32)
    return uintAsFloat(entry.distBits);
}

fn decodePointID(entry: EncodedEntry) -> i32 {
    // 对应CPU代码：int(uint32_t(v))
    return bitcast<i32>(entry.pointIDBits);
}

// 比较两个编码条目（用于排序）
fn compareEntries(a: EncodedEntry, b: EncodedEntry) -> bool {
    // 首先比较距离
    if (a.distBits != b.distBits) {
        return a.distBits < b.distBits;
    }
    // 如果距离相同，比较点ID
    return a.pointIDBits < b.pointIDBits;
}

// KDTree候选列表结构（对应CPU版本的FixedCandidateList）
struct FixedCandidateList {
    entry: array<EncodedEntry, 5>,  // 对应CPU的uint64_t entry[k]
    cutOffRadius2: f32,
    k: i32,
};

// 初始化候选列表（对应CPU的FixedCandidateList构造函数）
fn initCandidateList(cutOffRadius: f32, k: i32) -> FixedCandidateList {
    var list: FixedCandidateList;
    list.cutOffRadius2 = cutOffRadius * cutOffRadius;
    list.k = k;
    
    // 对应CPU代码：entry[i] = this->encode(cutOffRadius*cutOffRadius,-1);
    let initEntry = encode(list.cutOffRadius2, -1);
    for (var i = 0; i < 5; i++) {
        list.entry[i] = initEntry;
    }
    
    return list;
}

// 获取最大半径（对应CPU的maxRadius2）
fn maxRadius2(list: ptr<function, FixedCandidateList>) -> f32 {
    // 对应CPU代码：return this->decode_dist2(entry[k-1]);
    return decodeDist2((*list).entry[(*list).k - 1]);
}

// 获取距离（对应CPU的get_dist2）
fn getDist2(list: ptr<function, FixedCandidateList>, i: i32) -> f32 {
    return decodeDist2((*list).entry[i]);
}

// 获取点ID（对应CPU的get_pointID）
fn getPointID(list: ptr<function, FixedCandidateList>, i: i32) -> i32 {
    return decodePointID((*list).entry[i]);
}

// push函数（对应CPU的FixedCandidateList::push）
fn push(list: ptr<function, FixedCandidateList>, dist: f32, pointID: i32) {
    let v = encode(dist, pointID);
    
    // 对应CPU代码中的核心逻辑：
    // for (int i=0;i<k;i++) {
    //     uint64_t vmax = std::max(entry[i],v);
    //     uint64_t vmin = std::min(entry[i],v);
    //     entry[i] = vmin;
    //     v = vmax;
    // }
    var currentV = v;
    for (var i = 0; i < (*list).k; i++) {
        let entrySmaller = compareEntries((*list).entry[i], currentV);
        var vmax: EncodedEntry;
        var vmin: EncodedEntry;
        
        if (entrySmaller) {
            vmin = (*list).entry[i];
            vmax = currentV;
        } else {
            vmin = currentV;
            vmax = (*list).entry[i];
        }
        
        (*list).entry[i] = vmin;
        currentV = vmax;
    }
}

// 处理候选点（对应CPU的processCandidate）
fn processCandidate(list: ptr<function, FixedCandidateList>, candPrimID: i32, candDist2: f32) -> f32 {
    push(list, candDist2, candPrimID);
    return maxRadius2(list);
}

// 计算二维点之间的平方距离（对应CPU的sqrDistance）
fn sqrDistance2D(p1: vec2<f32>, p2: vec2<f32>) -> f32 {
    let d = p1 - p2;
    return dot(d, d);
}

// 获取坐标的特定维度值（对应CPU的get_coord）
fn getCoord(point: vec2<f32>, dim: i32) -> f32 {
    // 对应CPU代码：inline float get_coord(const float2 &v, int d) { return d ? v.y : v.x; }
    return select(point.x, point.y, dim != 0);
}

// KDTree遍历函数（严格对应CPU版本的traverse_stack_free）
fn kdTreeTraverseStackFree(
    result: ptr<function, FixedCandidateList>, 
    queryPoint: vec2<f32>, 
    N: i32,
    eps: f32
) {
    let epsErr = 1.0 + eps;
    let numDims = 2;
    var cullDist = 500.0;
    
    var prev = -1;
    var curr = 0;
    
    loop {
        let parent = (curr + 1) / 2 - 1;
        
        if (curr >= N) {
            // in some (rare) cases it's possible that below traversal
            // logic will go to a "close child", but may actually only
            // have a far child. In that case it's easiest to fix this
            // right here, pretend we've done that (non-existent) close
            // child, and let parent pick up traversal as if it had been
            // done.
            prev = curr;
            curr = parent;
            continue;
        }
        
        let currNode = kdTreePoints[curr];
        let child = 2 * curr + 1;
        let fromChild = (prev >= child);
        
        if (!fromChild) {
            let currPoint = vec2<f32>(currNode.x, currNode.y);
            let sqrDist = sqrDistance2D(queryPoint, currPoint);
            cullDist = processCandidate(result, curr, sqrDist);
        }
        
        // 计算当前维度：对应CPU代码中的 BinaryTree::levelOf(curr) % num_dims
        let currDim = levelOf(curr) % numDims;
        
        let currDimDist = getCoord(queryPoint, currDim) - getCoord(vec2<f32>(currNode.x, currNode.y), currDim);
        
        let currSide = select(0, 1, currDimDist > 0.0);
        let currCloseChild = 2 * curr + 1 + currSide;
        let currFarChild = 2 * curr + 2 - currSide;
        
        var next = -1;
        
        if (prev == currCloseChild) {
            // if we came from the close child, we may still have to check
            // the far side - but only if this exists, and if far half of
            // current space if even within search radius.
            if ((currFarChild < N) && (currDimDist * currDimDist * epsErr < cullDist)) {
                next = currFarChild;
            } else {
                next = parent;
            }
        } else if (prev == currFarChild) {
            // if we did come from the far child, then both children are
            // done, and we can only go up.
            next = parent;
        } else {
            // we didn't come from any child, so must be coming from a
            // parent... we've already been processed ourselves just now,
            // so next stop is to look at the children (unless there
            // aren't any). this still leaves the case that we might have
            // a child, but only a far child, and this far child may or
            // may not be in range ... we'll fix that by just going to
            // near child *even if* only the far child exists, and have
            // that child do a dummy traversal of that missing child, then
            // pick up on the far-child logic when we return.
            if (child < N) {
                next = currCloseChild;
            } else {
                next = parent;
            }
        }
        
        if (next == -1) {
            // if (curr == 0 && from_child)
            // this can only (and will) happen if and only if we come from a
            // child, arrive at the root, and decide to go to the parent of
            // the root ... while means we're done.
            return;
        }
        
        prev = curr;
        curr = next;
    }
}

// KDTree KNN搜索函数（对应CPU的knn函数）
fn kdTreeKNNSearch(queryPoint: vec2<f32>, k: i32, searchRadius: f32) -> FixedCandidateList {
    var result = initCandidateList(searchRadius, k);
    
    // 确保有节点可以搜索
    if (uniforms.totalNodes > 0u) {
        kdTreeTraverseStackFree(&result, queryPoint, i32(uniforms.totalNodes), 0.0);
    }
    
    return result;
}

// 使用KDTree的最近邻插值
fn kdTreeNearestNeighborInterpolation(dataPos: vec2<f32>) -> f32 {
    var knnResult = kdTreeKNNSearch(dataPos, 1, uniforms.searchRadius);
    
    let pointID = getPointID(&knnResult, 0);
    if (pointID >= 0 && pointID < i32(uniforms.totalNodes)) 
    {
        let value = kdTreePoints[pointID].value;
        let val2 = bruteNearestKD(dataPos);
        let err = abs(value - val2);
        return val2;
    }
    
    return -1.0;
}

// 使用KDTree的KNN质心插值（反距离加权）
fn kdTreeKNNCentroidInterpolation(dataPos: vec2<f32>) -> f32 {
    var knnResult = kdTreeKNNSearch(dataPos, 3, 500.0);
    
    let firstPointID = getPointID(&knnResult, 0);
    if (firstPointID < 0) {
        return -1.0;
    }
    
    // 检查是否有重合点
    let firstDist2 = getDist2(&knnResult, 0);
    if (firstDist2 < 0.0001) {
        return kdTreePoints[firstPointID].value;
    }
    
    var weightedSum = 0.0;
    var weightSum = 0.0;
    
    for (var i = 0; i < 3; i++) {
        let pointID = getPointID(&knnResult, i);
        if (pointID >= 0 && pointID < i32(uniforms.totalNodes)) {
            let dist2 = getDist2(&knnResult, i);
            if (dist2 > 0.0001) {  // 避免除零
                let weight = 1.0 / dist2;
                weightedSum += kdTreePoints[pointID].value * weight;
                weightSum += weight;
            }
        }
    }
    
    if (weightSum > 0.0) {
        return weightedSum / weightSum;
    }
    
    return -1.0;
}

// 栈条目结构
struct StackEntry {
    nodeID: i32,
    minBounds: vec2<f32>,
    maxBounds: vec2<f32>,
    splitDim: i32,
};

// 固定大小的栈
struct TraversalStack {
    entries: array<StackEntry, 64>,  // 32层深度应该足够
    top: i32,
};

// 初始化栈
fn initStack() -> TraversalStack {
    var stack: TraversalStack;
    stack.top = -1;
    return stack;
}

// 入栈
fn pushStack(stack: ptr<function, TraversalStack>, entry: StackEntry) -> bool {
    if ((*stack).top < 31) {
        (*stack).top++;
        (*stack).entries[(*stack).top] = entry;
        return true;
    }
    return false;  // 栈满
}

// 出栈
fn popStack(stack: ptr<function, TraversalStack>) -> StackEntry {
    var entry: StackEntry;
    if ((*stack).top >= 0) {
        entry = (*stack).entries[(*stack).top];
        (*stack).top--;
    } else {
        entry.nodeID = -1;  // 标记为无效
    }
    return entry;
}

// 检查栈是否为空
fn isStackEmpty(stack: ptr<function, TraversalStack>) -> bool {
    return (*stack).top < 0;
}

// 获取左子节点ID
fn getLeftChild(nodeID: i32, totalNodes: i32) -> i32 {
    let leftChild = 2 * nodeID + 1;
    return select(-1, leftChild, leftChild < totalNodes);
}

// 获取右子节点ID
fn getRightChild(nodeID: i32, totalNodes: i32) -> i32 {
    let rightChild = 2 * nodeID + 2;
    return select(-1, rightChild, rightChild < totalNodes);
}

// 计算点到边界框的最小距离平方
fn distanceToBox(point: vec2<f32>, minBounds: vec2<f32>, maxBounds: vec2<f32>) -> f32 {
    let dx = max(0.0, max(minBounds.x - point.x, point.x - maxBounds.x));
    let dy = max(0.0, max(minBounds.y - point.y, point.y - maxBounds.y));
    return dx * dx + dy * dy;
}

// 左序平衡KDTree遍历（纯栈版本，无递归）
fn kdTreeTraverseLeftOrder(
    result: ptr<function, FixedCandidateList>,
    queryPoint: vec2<f32>,
    N: i32,
    worldBounds: vec4<f32>  // (minX, minY, maxX, maxY)
) {
    if (N <= 0) {
        return;
    }
    
    var stack = initStack();
    
    // 初始化根节点并入栈
    var rootEntry: StackEntry;
    rootEntry.nodeID = 0;
    rootEntry.minBounds = vec2<f32>(worldBounds.x, worldBounds.y);
    rootEntry.maxBounds = vec2<f32>(worldBounds.z, worldBounds.w);
    rootEntry.splitDim = 0;  // 根节点从x维度开始
    
    if (!pushStack(&stack, rootEntry)) {
        return;  // 栈满，无法继续
    }
    
    var iterationCount = 0;
    let maxIterations = N * 3;  // 防止无限循环
    
    // 主循环：栈不为空就继续处理
    while (!isStackEmpty(&stack) && iterationCount < maxIterations) {
        iterationCount++;
        
        // 从栈中取出一个节点
        let current = popStack(&stack);
        if (current.nodeID < 0 || current.nodeID >= N) {
            continue;
        }
        
        // 获取当前节点
        let currentNode = kdTreePoints[current.nodeID];
        let currentPoint = vec2<f32>(currentNode.x, currentNode.y);
        
        // 处理当前节点（加入候选列表）
        let sqrDist = sqrDistance2D(queryPoint, currentPoint);
        let cullDist = processCandidate(result, current.nodeID, sqrDist);
        
        // 计算分割平面
        let splitValue = getCoord(currentPoint, current.splitDim);
        let queryValue = getCoord(queryPoint, current.splitDim);
        
        // 获取子节点
        let leftChild = getLeftChild(current.nodeID, N);
        let rightChild = getRightChild(current.nodeID, N);
        
        // 如果没有子节点，继续下一个栈条目
        if (leftChild < 0 && rightChild < 0) {
            continue;
        }
        
        // 计算左右子空间的边界
        var leftMinBounds = current.minBounds;
        var leftMaxBounds = current.maxBounds;
        var rightMinBounds = current.minBounds;
        var rightMaxBounds = current.maxBounds;
        
        if (current.splitDim == 0) {
            // 按x分割
            leftMaxBounds.x = splitValue;
            rightMinBounds.x = splitValue;
        } else {
            // 按y分割
            leftMaxBounds.y = splitValue;
            rightMinBounds.y = splitValue;
        }
        
        // 计算下一层的分割维度
        let nextSplitDim = (current.splitDim + 1) % 2;
        
        // 决定访问顺序：先访问查询点所在的一侧
        let queryOnLeft = queryValue <= splitValue;
        
        // 计算到各个子空间的最小距离
        let leftMinDist2 = distanceToBox(queryPoint, leftMinBounds, leftMaxBounds);
        let rightMinDist2 = distanceToBox(queryPoint, rightMinBounds, rightMaxBounds);
        
        // 策略：先入栈远的子节点，后入栈近的子节点
        // 这样栈顶总是最近的子节点，优先处理
        
        if (queryOnLeft) {
            // 查询点在左侧，右侧是远侧
            if (rightChild >= 0 && rightMinDist2 < cullDist) {
                var rightEntry: StackEntry;
                rightEntry.nodeID = rightChild;
                rightEntry.minBounds = rightMinBounds;
                rightEntry.maxBounds = rightMaxBounds;
                rightEntry.splitDim = nextSplitDim;
                pushStack(&stack, rightEntry);
            }
            
            // 左侧是近侧，后入栈（优先处理）
            if (leftChild >= 0 && leftMinDist2 < cullDist) {
                var leftEntry: StackEntry;
                leftEntry.nodeID = leftChild;
                leftEntry.minBounds = leftMinBounds;
                leftEntry.maxBounds = leftMaxBounds;
                leftEntry.splitDim = nextSplitDim;
                pushStack(&stack, leftEntry);
            }
        } else {
            // 查询点在右侧，左侧是远侧
            if (leftChild >= 0 && leftMinDist2 < cullDist) {
                var leftEntry: StackEntry;
                leftEntry.nodeID = leftChild;
                leftEntry.minBounds = leftMinBounds;
                leftEntry.maxBounds = leftMaxBounds;
                leftEntry.splitDim = nextSplitDim;
                pushStack(&stack, leftEntry);
            }
            
            // 右侧是近侧，后入栈（优先处理）
            if (rightChild >= 0 && rightMinDist2 < cullDist) {
                var rightEntry: StackEntry;
                rightEntry.nodeID = rightChild;
                rightEntry.minBounds = rightMinBounds;
                rightEntry.maxBounds = rightMaxBounds;
                rightEntry.splitDim = nextSplitDim;
                pushStack(&stack, rightEntry);
            }
        }
    }
}

// 修改的KNN搜索函数
fn kdTreeKNNSearchLeftOrder(queryPoint: vec2<f32>, k: i32, searchRadius: f32, worldBounds: vec4<f32>) -> FixedCandidateList {
    var result = initCandidateList(searchRadius, k);
    
    if (uniforms.totalNodes > 0u) {
        kdTreeTraverseLeftOrder(&result, queryPoint, i32(uniforms.totalNodes), worldBounds);
    }
    
    return result;
}


// 更新的最近邻插值函数
fn kdTreeNearestNeighborInterpolationLeftOrder(dataPos: vec2<f32>) -> f32 {
    // 设置世界边界
    let worldBounds = vec4<f32>(0.0, 0.0, uniforms.gridWidth, uniforms.gridHeight);
    
    var knnResult = kdTreeKNNSearchLeftOrder(dataPos, 1, uniforms.searchRadius, worldBounds);
    
    let pointID = getPointID(&knnResult, 0);
    if (pointID >= 0 && pointID < i32(uniforms.totalNodes)) {
        let kdtreeValue = kdTreePoints[pointID].value;
        let bruteValue = bruteNearestKD(dataPos);
        let err = abs(kdtreeValue - bruteValue);  // 返回差值用于验证
        return kdtreeValue;
    }
    
    return -1.0;
}


fn interpolateValue(dataPos: vec2<f32>) -> f32 {
    if (uniforms.interpolationMethod == 0u) {
        return kdTreeNearestNeighborInterpolation(dataPos);
    }
    else if (uniforms.interpolationMethod == 1u) {
        return kdTreeNearestNeighborInterpolation(dataPos);
    }
    return 0.0;
}




@compute @workgroup_size(16, 16)
fn main(@builtin(global_invocation_id) global_id: vec3<u32>) {
    let dims = textureDimensions(outputTexture);
    
    // 边界检查
    if (global_id.x >= dims.x || global_id.y >= dims.y) {
        return;
    }
    
    // 当前像素在纹理空间
    let pixelCoord = vec2<f32>(f32(global_id.x), f32(global_id.y));
    let uv = pixelCoord / vec2<f32>(f32(dims.x), f32(dims.y));
    
    // 映射到数据空间
    let dataPos = vec2<f32>(
        uv.x * uniforms.gridWidth,
        uv.y * uniforms.gridHeight
    );
    
    // 使用选定的插值方法
    let interpolatedValue = interpolateValue(dataPos);
    
    var color = vec4<f32>(1.0, 0.0, 0.0, 1.0); // 默认红色（未找到数据）
    
    if (interpolatedValue != -1.0) {
        let normalized = clamp(
            (interpolatedValue - uniforms.minValue) / (uniforms.maxValue - uniforms.minValue),
            0.0, 1.0
        );
        color = getColorFromTF(normalized);
    }

    // color = vec4<f32>(interpolatedValue, interpolatedValue, interpolatedValue, 1.0);
    
    textureStore(outputTexture, vec2<i32>(global_id.xy), color);
}